@str; @rex; @case
/*
	Поиск в строке с использованием регулярных выражений. 
	В отличие от стандартной функции REGEXPFIND 
	эта функция возвращает все вхождения.
	
	**Параметры**
		@str - строка, в которой осуществляется поиск;
		@rex - шаблон поиска, обычная строка или регулярное выражедение;
		@case - флаг учета регистра при поиске: 1 - учитывать регистр, 
		0 - не учитывать. По умолчанию - 0.
		
	**Возвращаемое значение**
		- если подстроки, соответствующие шаблону найдены, 
			функция возвращает массив:
			[1] - число найденных подстрок; 
			[2] - номер символа, с которого начинается вхождение первой подстроки
			[3] - длина первой подстроки;
			[4] - номер символа, с которого начинается вхождение 2-й подстроки
			[5] - длина 2-й подстроки;
		- если совпадений не найдено, функция возвращает 0;
		- в случае ошибки (некорректный шаблон поиска), функция возвращает -1 
			и описание ошибки.
*/

@ret:=0
@pos:=1
while @pos <= len(@str) do [
	@a:=regexpfind(@str,@pos,@rex,@case)
	if @a = -1 then [
		goto end
	]
	else if @a = 0 then [
		@ret:=-1
		@ret(-1):=@@ErrorText
		goto end
	]
	else [
		@ret[1]:=@ret[1]+1
		@ret(-1):=@a[1]
		@ret(-1):=@a[2]
		@pos:=@a[1]+@a[2]
	]	
]

end:
